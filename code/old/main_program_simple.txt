# SERVOS
# LEFT = servo 0
# RIGHT = servo 1
# MIDDLE= servo 2
# LEFT SENSOR = servo 3
# right SENSOR = servo 4



# Main
start:
# Start by setting the legs to middle position for 3 seconds
5696 0 servo    
5696 1 servo
5320 2 servo 
moving_wait
3000 delay

loop:
  forward
  goto loop

###############################################
###############################################

sub moving_wait
  begin
    get_moving_state
  while
    # wait until it is no longer moving
  repeat
return

# forward
sub forward
 4440 2 servo # middle, left down
 500 delay
 6496 0 servo # left fwd   
 6496 1 servo #right bwd
 1000 delay

 6200 2 servo # middle, right down
 500 delay
 4896 0 servo # left bwd   
 4896 1 servo #right fwd
 1000 delay  
return

######################################3

# returns true if the left sensor sees an object
sub left_sensor
 3 get_position 400 greater_than  return
return


# returns true if the right sensor sees an object
sub right_sensor
 4 get_position 400 greater_than  return
return

# back
sub backward
 6096 2 servo # middle, left down
 500 delay
 4896 0 servo # left bwd   
 4896 1 servo #right fwd

 1000 delay

 5296 2 servo # middle, right down
 500 delay
 6496 0 servo # left fwd   
 6496 1 servo #right bwd
 1000 delay  
return

# right
sub rightward
 6096 2 servo # middle, left down
 500 delay
 6496  0 servo # left fwd
 4896 1 servo #right fwd
 
 1000 delay

 5296 2 servo # middle, right down
 500 delay
 4896 0 servo # left bwd   
 6496 1 servo #right bwd
 1000 delay  
return

# left
sub leftward
 5296 2 servo # middle, right down
 500 delay
 6496 0 servo # left fwd
 4896 1 servo #right fwd
   
 1000 delay
 
 6096 2 servo # middle, left down
 500 delay
 4896 0 servo # left bwd   
 6496 1 servo #right bwd
 1000 delay  
return

